apply plugin: 'base'

ext.configFile = file "build.properties"
configFile.withReader {
    // read config.  it shall from now on be referenced as simply config or as project.config
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}
buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'com.qcloud:cos_api:5.4.6'
    }
}

subprojects {
    if (!project.name.toLowerCase().contains("forge"))
        apply from: rootProject.file('common.gradle')
}

/* COS Upload Begin */
import com.qcloud.cos.COSClient
import com.qcloud.cos.ClientConfig
import com.qcloud.cos.auth.BasicCOSCredentials
import com.qcloud.cos.auth.COSCredentials
import com.qcloud.cos.region.Region
import com.qcloud.cos.model.PutObjectRequest

static String getLibKey(String filename) {
    return String.format(
            "libraries/customskinloader/%s/%s/%s",
            filename.substring(0, filename.lastIndexOf('.')),
            filename.substring(filename.lastIndexOf('.') + 1),
            filename
    )
}

static void uploadFileToCOS(File file) {
    COSCredentials cred = new BasicCOSCredentials(System.getenv("COS_SECRET_ID"), System.getenv("COS_SECRET_KEY"))
    ClientConfig clientConfig = new ClientConfig(new Region("ap-shanghai"))
    COSClient cosClient = new COSClient(cred, clientConfig)
    String bucketName = System.getenv("COS_BUCKET")
    String key = getLibKey(file.getName())

    PutObjectRequest putObjectRequest = new PutObjectRequest(bucketName, key, file)
    cosClient.putObject(putObjectRequest)
    println("Uploaded to COS: " + key)
}

task upload {
    doLast {
        File dir = rootProject.file("build/libs")
        if (!dir.isDirectory()) return
        File[] files = dir.listFiles()
        for (file in files) {
            uploadFileToCOS(file)
        }
    }
}
/* COS Upload End */